{
  parserImports="static com.intellij.lang.WhitespacesBinders.*"
  parserClass="com.intellij.dts.lang.parser.DtsParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  generate=[visitor="no" fqn="yes"]

  psiClassPrefix="Dts"
  psiImplClassSuffix="Impl"
  psiPackage="com.intellij.dts.lang.psi"
  psiImplPackage="com.intellij.dts.lang.psi.impl"

  elementTypeHolderClass="com.intellij.dts.lang.psi.DtsTypes"
  elementTypeClass="com.intellij.dts.lang.DtsElementType"

  tokenTypeClass="com.intellij.dts.lang.DtsTokenType"
  parserUtilClass="com.intellij.dts.lang.parser.DtsParserUtil"

  implements("rootNode")="com.intellij.dts.lang.psi.DtsNode.Root"
  mixin("rootNode")="com.intellij.dts.lang.psi.mixin.DtsRootNodeMixin"
  stubClass("rootNode")="com.intellij.dts.lang.stubs.impl.DtsRootNodeStub"
  elementTypeFactory("rootNode")="com.intellij.dts.lang.stubs.DtsStubElementTypes.factory"

  implements("refNode")="com.intellij.dts.lang.psi.DtsNode.Ref"
  mixin("refNode")="com.intellij.dts.lang.psi.mixin.DtsRefNodeMixin"
  stubClass("refNode")="com.intellij.dts.lang.stubs.impl.DtsRefNodeStub"
  elementTypeFactory("refNode")="com.intellij.dts.lang.stubs.DtsStubElementTypes.factory"

  implements("subNode")="com.intellij.dts.lang.psi.DtsNode.Sub"
  mixin("subNode")="com.intellij.dts.lang.psi.mixin.DtsSubNodeMixin"
  stubClass("subNode")="com.intellij.dts.lang.stubs.impl.DtsSubNodeStub"
  elementTypeFactory("subNode")="com.intellij.dts.lang.stubs.DtsStubElementTypes.factory"

  implements("nodeContent")="com.intellij.dts.lang.psi.DtsContainer"
  mixin("nodeContent")="com.intellij.dts.lang.psi.mixin.DtsNodeContentMixin"

  implements("property")="com.intellij.dts.lang.psi.DtsStatement.Property"
  mixin("property")="com.intellij.dts.lang.psi.mixin.DtsPropertyMixin"

  implements("compilerDirective")="com.intellij.dts.lang.psi.DtsStatement.CompilerDirective"
  mixin("compilerDirective")="com.intellij.dts.lang.psi.mixin.DtsCompilerDirectiveMixin"

  implements("entry")="com.intellij.dts.lang.psi.mixin.IDtsEntry"
  mixin("entry")="com.intellij.dts.lang.psi.mixin.DtsEntryMixin"

  implements("cellArray")="com.intellij.dts.lang.psi.DtsArray.Cell"
  mixin("cellArray")="com.intellij.dts.lang.psi.mixin.DtsCellArrayMixin"

  implements("cellArrayBits")="com.intellij.dts.lang.psi.mixin.IDtsCellArrayBits"
  mixin("cellArrayBits")="com.intellij.dts.lang.psi.mixin.DtsCellArrayBitsMixin"

  implements("byteArray")="com.intellij.dts.lang.psi.DtsArray.Byte"
  mixin("byteArray")="com.intellij.dts.lang.psi.mixin.DtsByteArrayMixin"

  implements("int")="com.intellij.dts.lang.psi.DtsValue.Int"
  mixin("int")="com.intellij.dts.lang.psi.mixin.DtsIntMixin"

  implements("byte")="com.intellij.dts.lang.psi.DtsValue.Byte"
  mixin("byte")="com.intellij.dts.lang.psi.mixin.DtsByteMixin"

  implements("exprValue")="com.intellij.dts.lang.psi.DtsValue.Expression"
  implements("exprValue")="com.intellij.dts.lang.psi.DtsValue.Int"
  mixin("exprValue")="com.intellij.dts.lang.psi.mixin.DtsExpressionMixin"

  implements("ppMacro")="com.intellij.dts.lang.psi.DtsValue.Macro"

  implements("string")="com.intellij.dts.lang.psi.DtsValue.String"
  mixin("string")="com.intellij.dts.lang.psi.mixin.DtsStringMixin"

  implements("char")="com.intellij.dts.lang.psi.DtsValue.Char"
  mixin("char")="com.intellij.dts.lang.psi.mixin.DtsCharMixin"

  implements("pHandle")="com.intellij.dts.lang.psi.DtsValue.PHandle"
  mixin("pHandle")="com.intellij.dts.lang.psi.mixin.DtsPHandleMixin"

  implements("includeStatement")="com.intellij.dts.lang.psi.PsiFileInclude"
  mixin("includeStatement")="com.intellij.dts.lang.psi.mixin.DtsIncludeStatementMixin"

  extends(".*Expr")=expr
  consumeTokenMethod(".*Recover")="consumeTokenFast"

  hooks("nodeContent")=[rightBinder="GREEDY_RIGHT_BINDER" leftBinder="GREEDY_LEFT_BINDER"]
  hooks("entry")=[rightBinder="trailingCommentsBinder" leftBinder="leadingCommentsBinder"]

  // c preprocessor rules

  implements("ppIncludeStatement")="com.intellij.dts.lang.psi.PsiFileInclude"
  mixin("ppIncludeStatement")="com.intellij.dts.lang.psi.mixin.PpIncludeStatementMixin"

  tokens=[
    COMMENT_EOL="COMMENT_EOL"
    COMMENT_C="COMMENT_C"
    PP_STATEMENT_MARKER="PP_STATEMENT_MARKER"
    PP_MACRO_ARG="PP_MACRO_ARG"
  ]
}

file ::= entries*

private entries ::=
        <<entry property>> |
        <<entry subNode>> |
        <<entry refNode>> |
        <<entry rootNode>> |
        <<compilerDirectiveEntry V1 ()>> |
        <<compilerDirectiveEntry PLUGIN ()>> |
        <<compilerDirectiveEntry (LABEL* MEMRESERVE) (cell cell)>> |
        <<compilerDirectiveEntry DELETE_NODE (pHandle | NAME)>> |
        <<compilerDirectiveEntry DELETE_PROP NAME>> |
        <<compilerDirectiveEntry OMIT_NODE pHandle>> |
        invalidEntry

external invalidEntry ::= parseInvalidEntry

rootNode ::= SLASH LBRACE nodeContent? RBRACE {pin=2 recoverWhile="rootNodeRecover"}
refNode ::= LABEL* pHandle LBRACE nodeContent? RBRACE {pin=3 recoverWhile="rootNodeRecover"}
private rootNodeRecover ::= !(SEMICOLON | RBRACE | HANDLE | afterLineBreak)

subNode ::= LABEL* OMIT_NODE? LABEL* NAME LBRACE nodeContent? RBRACE {pin=5 recoverWhile="subNodeRecover"}
private subNodeRecover ::= !(SEMICOLON | RBRACE | NAME | afterLineBreak)

nodeContent ::= (!RBRACE entries)+

property ::= LABEL* propertyName propertyAssignment?
private propertyAssignment ::= ASSIGN propertyContent {pin=1}
propertyContent ::= propertyValue (COMMA propertyValue)* {recoverWhile="propertyRecover"}
private propertyValue ::= <<value (string | pHandle | cellArray | byteArray | ppMacro)>>
private propertyRecover ::= !(SEMICOLON | RBRACE | NAME | afterLineBreak)

external propertyName ::= parsePropertyName

cellArray ::= (&BITS cellArrayBits)? LANGL cellArrayContent RANGL {pin=2}
private cellArrayContent ::= cellArrayValue* {recoverWhile="cellArrayRecover"}
private cellArrayValue ::= <<value (pHandle | cell)>>
private cellArrayRecover ::= !(SEMICOLON | RBRACE | COMMA | RANGL | RPAREN)

cellArrayBits ::= BITS (int | ppMacro) {pin=1 recoverWhile="cellArrayBitsRecover"}
private cellArrayBitsRecover ::= !(SEMICOLON | RBRACE | LANGL)

byteArray ::= LBRACKET byteArrayContent RBRACKET {pin=1}
private byteArrayContent ::= byteArrayValue* {recoverWhile="byteArrayRecover"}
private byteArrayValue ::= <<value (byte | ppMacro)>>
private byteArrayRecover ::= !(SEMICOLON | RBRACE | COMMA | RBRACKET | RPAREN)

pHandle ::= HANDLE (NAME | pHandlePath) {pin=1}
private pHandlePath ::= LBRACE path RBRACE {pin=1}
private path ::= PATH {recoverWhile="pathRecover"}
private pathRecover ::= !(SEMICOLON | RANGL | RBRACE | afterLineBreak)

meta entry ::= <<arg>> SEMICOLON {pin=1}
private meta value ::= LABEL* <<arg>> trailingLabels?

external trailingLabels ::= parseTrailingLabels

private meta compilerDirectiveEntry ::= <<entry <<compilerDirective <<directive>> <<arg>>>>>>
meta compilerDirective ::= <<directive>> <<arg>> {pin=1 recoverWhile="compilerDirectiveRecover"}
private compilerDirectiveRecover ::= !(SEMICOLON | RBRACE | afterLineBreak)

private cell ::= int | char | exprValue | ppMacro

byte ::= BYTE_LITERAL
int ::= INT_LITERAL
string ::= STRING_LITERAL
char ::= CHAR_LITERAL

exprValue ::= LPAREN expr RPAREN {pin=1}

expr ::= ppMacroExpr
    | ternaryExpr
    | lOrExpr
    | lAndExpr
    | bOrExpr
    | xorExpr
    | bAndExpr
    | eqGroup
    | relaGroup
    | shiftGroup
    | addGroup
    | mulGroup
    | unaryGroup
    | primaryGroup

private eqGroup ::= eqExpr | neqExpr
private relaGroup ::= lesExpr | grtExpr | leqExpr | geqExpr
private shiftGroup ::= lshExpr | rshExpr
private addGroup ::= addExpr | subExpr
private mulGroup ::= mulExpr | divExpr | modExpr
private unaryGroup ::= lNotExpr | bNotExpr | negExpr
private primaryGroup ::= intExpr | charExpr | parenExpr

ppMacroExpr ::= ppMacro
ternaryExpr ::= expr TERNARY expr COLON expr

addExpr ::= expr ADD expr
subExpr ::= expr SUB expr
mulExpr ::= expr MUL expr
divExpr ::= expr DIV expr
modExpr ::= expr MOD expr

bAndExpr ::= expr AND expr
lAndExpr ::= expr L_AND expr

bOrExpr ::= expr OR expr
lOrExpr ::= expr L_OR expr

bNotExpr ::= NOT expr
lNotExpr ::= L_NOT expr
negExpr ::= SUB expr

xorExpr ::= expr XOR expr
lshExpr ::= expr LSH expr
rshExpr ::= expr RSH expr

lesExpr ::= expr LES expr
grtExpr ::= expr GRT expr
leqExpr ::= expr LEQ expr
geqExpr ::= expr GEQ expr
eqExpr ::= expr EQ expr
neqExpr ::= expr NEQ expr

intExpr ::= int
charExpr ::= char
parenExpr ::= LPAREN expr RPAREN

includeStatement ::= () INCLUDE INCLUDE_PATH { pin=1 }

external afterLineBreak ::= parseAfterLineBreak

// fallback to handle unknown identifiers like macros
ppMacro ::= parserPpMacro
external parserPpMacro ::= parsePpMacro

// c preprocessor rules
private ppStatement ::= () (ppStatements) PP_STATEMENT_END { pin=1 }

private ppStatements ::=
    ppIncludeStatement |
    ppIfdefStatement |
    ppIfndefStatement |
    ppEndifStatement |
    ppDefineStatement |
    ppUndefStatement |
    ppIfStatement |
    ppElifStatement |
    ppElseStatement
{ recoverWhile="pp_statementRecover" }

private pp_statementRecover ::= !(PP_STATEMENT_END)

ppIncludeStatement ::= PP_INCLUDE PP_INCLUDE_PATH { pin=1 }

ppIfdefStatement ::= PP_IFDEF PP_SYMBOL { pin=1 }
ppIfndefStatement ::= PP_IFNDEF PP_SYMBOL { pin=1 }
ppIfStatement ::= PP_IF PP_EXPRESSION { pin=1 }
ppElifStatement ::= PP_ELIF PP_EXPRESSION { pin=1 }
ppElifdefStatement ::= PP_ELIFDEF PP_SYMBOL { pin=1 }
ppElifndefStatement ::= PP_ELIFNDEF PP_SYMBOL { pin=1 }
ppElseStatement ::= PP_ELSE
ppEndifStatement ::= PP_ENDIF
ppDefineStatement ::= PP_DEFINE PP_SYMBOL PP_DEFINE_VALUE? { pin=1 }
ppUndefStatement ::= PP_UNDEF PP_SYMBOL { pin=1 }